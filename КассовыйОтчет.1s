
Функция   Сформировать()
	
	тхт = "
	
	//*******************************************************************
	|выбрать
	|	тт.Касса КАК Касса,
	|	Неопределено КАК Регистратор,
	|	Неопределено как Дата,
	|	Неопределено как Сддс,
	|	тт.СуммаОстаток КАК Нач,
	|	0 КАК Прих,
	|	0 КАК Расх,
	|	0 КАК Кон
	|поместить
	|	ВТ_ДАННЫЕ
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваНаличные.Остатки(
	|		&ДатаНачала,
	|		Касса = &Касса или &Касса = Неопределено) КАК тт
	
	|объединить все выбрать
	|	тт.Касса,
	|	Неопределено,
	|	Неопределено,
	|	Неопределено,
	|	0 КАК Нач,
	|	0 КАК Прих,
	|	0 КАК Расх,
	|	тт.СуммаОстаток КАК Кон
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваНаличные.Остатки(
	|		&ДатаОкончания,
	|		Касса = &Касса или &Касса = Неопределено) КАК тт
	
	|объединить все выбрать
	|	тт.Касса,
	|	тт.Регистратор,
	|	тт.Период как Дата,
	|	тт.СтатьяДвиженияДенежныхСредств,
	|	0 КАК Нач,
	|	выбор когда тт.ВидДвижения = значение(ВидДвиженияНакопления.Приход) Тогда
	|		тт.Сумма иначе 0 конец КАК Прих,
	|	выбор когда тт.ВидДвижения = значение(ВидДвиженияНакопления.Расход) Тогда
	|		тт.Сумма иначе 0 конец КАК Расх,
	|	0 КАК Кон
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваНаличные как тт
	|где
	|	(тт.Касса = &Касса или &Касса = Неопределено)
	|	и тт.Активность
	|	и тт.Период >= &ДатаНачала
	|	и тт.Период < &ДатаОкончания;
	
	//*******************************************************************
	|выбрать
	|	тт.Касса,
	|	тт.Дата,
	|	тт.Регистратор,
	|	тт.сддс,
	|	IsNull(тСпр.НомерСтроки, 0) как НомерСддс,
	|	тт.Нач, тт.Прих, тт.Расх, тт.Кон
	|поместить
	|	ВТ_ДАННЫЕ_3
	|из
	|	ВТ_ДАННЫЕ как тт
	|	левое соединение
	|	Справочник.НастройкаКассовогоОтчета.СДДС как тСпр
	|	по тт.сддс = тСпр.СтатьяДвиженияДенежныхСредств
	|
	|объединить все выбрать
	|	&Касса, Неопределено, Неопределено,
	|	тСпр.СтатьяДвиженияДенежныхСредств,
	|	тСпр.НомерСтроки, 0, 0, 0, 0
	|из
	|	Справочник.НастройкаКассовогоОтчета.СДДС как тСпр
	|где
	|	&Касса <> Неопределено
	|;
	
	//*******************************************************************
	|выбрать
	|	Касса, Дата, Регистратор, сддс,
	|	максимум(НомерСддс) как НомерСддс,
	|	сумма(Нач),
	|	сумма(Прих),
	|	сумма(Расх),
	|	сумма(Кон)
	|поместить
	|	ВТ_ДАННЫЕ_4
	|из
	|	ВТ_ДАННЫЕ_3
	|сгруппировать по
	|	Касса, сддс, Дата, Регистратор;
	
	//*******************************************************************
	|выбрать
	|	*
	|из ВТ_ДАННЫЕ_4
	|упорядочить по
	|	Касса, НомерСддс, сддс, Дата
	|автоупорядочивание
	|ИТОГИ
	|	сумма(Нач), сумма(Прих), сумма(Расх), сумма(Кон)
	|по Касса, НомерСддс, сддс
	|	";
	
	з = новый Запрос(тхт);
	з.УстановитьПараметр("ДатаНачала", НачалоДня(ДатаНачала));
	з.УстановитьПараметр("ДатаОкончания", КонецДня(ДатаОкончания)+1);
	з.УстановитьПараметр("Касса", НеПуст(Касса,Неопределено));
	
	з.МенеджерВременныхТаблиц = новый МенеджерВременныхТаблиц;
	
	выб_кас = з.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Касса");
	
	Объект = РеквизитФормыВЗначение("Отчет");
	мак = Объект.ПолучитьМакет("Макет");
	
	о = мак.ПолучитьОбласть("Заголовок");
	о.Параметры.ДатаНачала = Формат(ДатаНачала, "ДФ=dd.MM.yyyy");
	о.Параметры.ДатаОкончания = Формат(ДатаОкончания, "ДФ=dd.MM.yyyy");
	
	тд = новый ТабличныйДокумент;
	тд.Вывести(о, 1, , Истина);
	
	пока выб_кас.Следующий() цикл
		тд.НачатьАвтогруппировкуСтрок();
		
		о = мак.ПолучитьОбласть("Касса");
		о.Параметры.Касса = выб_кас.Касса;
		о.Параметры.Нач = выб_кас.Нач;
		о.Параметры.Прих = выб_кас.Прих;
		о.Параметры.Расх = выб_кас.Расх;
		о.Параметры.Кон = выб_кас.кон;
		
		тд.Вывести(о, 1, "Касса", РазворачиватьСтатьиДенежныхСредств);
		
		выб_НомСддс = выб_кас.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "НомерСддс");
		пока выб_НомСддс.Следующий() Цикл
			
			выб_сддс = выб_НомСддс.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Сддс");
			Пока выб_сддс.Следующий() Цикл
			
				Если НЕ ЗначениеЗаполнено(выб_сддс.сддс) и выб_сддс.НомерСддс = 0 Тогда
					Если выб_сддс.Прих = 0 и выб_сддс.Расх = 0 Тогда
						продолжить;
					КонецЕсли;
				КонецЕсли;
			
				о = мак.ПолучитьОбласть("СДДС");
				о.Параметры.НомерСддс = выб_сддс.НомерСддс;
				о.Параметры.СДДС = выб_сддс.СДДС;
				о.Параметры.Прих = выб_сддс.Прих;
				о.Параметры.Расх = выб_сддс.Расх;
				тд.Вывести(о, 2, "СДДС", Ложь);
			
				НомПП = 0;
				выб_док = выб_сддс.Выбрать();
				пока выб_док.Следующий() цикл
					
					рег = выб_док.Регистратор;
					Если не ЗначениеЗаполнено(рег) Тогда
						Если выб_док.Прих = 0 и выб_док.Расх = 0 Тогда
							продолжить;
						КонецЕсли;
					КонецЕсли;
					
					назв = СокрЛП(выб_док.сддс)+" "+СокрЛП(рег.Номер)+"/" + СокрЛП(рег.Дата);
					НомПП = НомПП + 1;
					
					о = мак.ПолучитьОбласть("Строка");
					о.Параметры.Регистратор = Рег;
					о.Параметры.НомПП = НомПП;
					о.Параметры.Операция = назв;
					о.Параметры.Прих = выб_док.Прих;
					о.Параметры.Расх = выб_док.Расх;
					тд.Вывести(о, 3, "Регистратор", Ложь);
				КонецЦикла;
				
			КонецЦикла;
		КонецЦикла;
		
		тд.ЗакончитьАвтогруппировкуСтрок();
	КонецЦикла;
	
	возврат тд;
КонецФункции

&НаКлиенте
Процедура кмдСформировать(Команда)
	ТабДокумент = Сформировать();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	уп = МойМдл.ЭтоУправляющий();
	
	Если НЕ уп Тогда
		Элементы.грПараметры.Доступность = Ложь;
		Отказ = не ЗначениеЗаполнено(Касса);
	КонецЕсли;
	
	ДатаНачала = НачалоДня(ТекущаяДата());
	ДатаОкончания = ДатаНачала;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	РазворачиватьСтатьиДенежныхСредств = Истина;
КонецПроцедуры
